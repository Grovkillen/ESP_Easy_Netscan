// COMPILE_OPTS|C:\Users\Jimmy\Desktop\ESP_Easy_Netscan.exe|C:\Users\Jimmy\Dropbox\Diverse\ESP-projekt\ESP_Easy_Netscan\ICON.ico|CONSOLE=0|INCLUDES=1| /NOSYSTRAY|RUNTIMES=1|BMPS=1
//Set IGNORESPACES to 1 to force script interpreter to ignore spaces.
//If using IGNORESPACES quote strings in {" ... "}
//Let>IGNORESPACES=1
Let>DEBUG_LEVEL=0

//Global variables (version is MAYOR.MINOR.MINIMAL)
Let>VERSION=0.00.012
Let>APP_TITLE=ESP Easy Netscan (%VERSION%)

//Start of program
GoSub>DIALOG_MAIN
GoSub>GET_LOCAL_IP
GoSub>GET_THE_IP_OCTETS
GoSub>CONCAT_THE_MAC
GoSub>ADD_DIALOG_HANDLERS
GoSub>DO_A_SCAN,0,0,0
Show>Dialog_MAIN
Wait>%TIME_TO_DISPLAY_MESSAGE%
SetDialogProperty>Dialog_MAIN,STATUS_INFO_BAR,Visible,False

//Loop to let dialog_MAIN stay alive and listen to interactions from user
Label>WAITING_FOR_A_WHILE
Let>WAIT_FOR_CROWS=0.02
Wait>%WAIT_FOR_CROWS%
Goto>WAITING_FOR_A_WHILE

//Sub routines...
SRT>DIALOG_MAIN
Dialog>Dialog_MAIN
object Dialog_MAIN: TForm
  Left = 247
  Top = 97
  HelpContext = 5000
  BorderIcons = [biSystemMenu]
  Caption = 'ESP Easy node scanner'
  ClientHeight = 670
  ClientWidth = 964
  Color = clBtnFace
  Font.Charset = DEFAULT_CHARSET
  Font.Color = clWindowText
  Font.Height = -11
  Font.Name = 'MS Sans Serif'
  Font.Style = []
  OldCreateOrder = True
  ShowHint = True
  OnTaskBar = False
  PixelsPerInch = 96
  TextHeight = 13
  object Panel1: TPanel
    Left = 10
    Top = 10
    Width = 944
    Height = 540
    TabOrder = 0
    object THE_GRID: tMSStringGrid
      Left = 1
      Top = 1
      Width = 942
      Height = 538
      Align = alClient
      DefaultColWidth = 120
      DefaultRowHeight = 30
      FixedCols = 0
      RowCount = 2
      Font.Charset = ANSI_CHARSET
      Font.Color = clBlack
      Font.Height = -21
      Font.Name = 'Agency FB'
      Font.Style = []
      Options = [goHorzLine, goRowSizing, goColSizing, goRowSelect]
      ParentFont = False
      TabOrder = 0
    end
  end
  object Panel2: TPanel
    Left = 10
    Top = 560
    Width = 944
    Height = 100
    TabOrder = 1
    object STATUS_INFO_BAR: TLabel
      Left = 8
      Top = 56
      Width = 102
      Height = 13
      Caption = 'STATUS_INFO_BAR'
    end
    object Button_Scan: tMSButton
      Left = 8
      Top = 6
      Width = 75
      Height = 25
      Caption = 'Fetch info'
      TabOrder = 0
      DoBrowse = False
      BrowseStyle = fbOpen
    end
    object Button_Deep_Scan: tMSButton
      Left = 88
      Top = 6
      Width = 75
      Height = 25
      Caption = 'Deep scan'
      TabOrder = 1
      DoBrowse = False
      BrowseStyle = fbOpen
    end
    object ProgressBar: TProgressBar
      Left = 6
      Top = 74
      Width = 931
      Height = 17
      Position = 33
      TabOrder = 3
    end
  end
end
EndDialog>Dialog_MAIN
END>DIALOG_MAIN

//Just kill the app
SRT>EXIT_APP
  Exit>
END>EXIT_APP

//Add dialog handlers...
SRT>ADD_DIALOG_HANDLERS
  SetDialogProperty>Dialog_MAIN,,Caption,APP_TITLE
  SetDialogProperty>Dialog_MAIN,ProgressBar,Position,0
  SetDialogProperty>Dialog_MAIN,STATUS_INFO_BAR,Visible,False
  AddDialogHandler>Dialog_MAIN,,OnClose,EXIT_APP
  AddDialogHandler>Dialog_MAIN,Button_Deep_Scan,OnClick,DO_A_SCAN(1,1,1)
  AddDialogHandler>Dialog_MAIN,Button_Scan,OnClick,DO_A_SCAN(0,0,1)
END>ADD_DIALOG_HANDLERS

SRT>DO_A_SCAN
  SetDialogProperty>Dialog_MAIN,Button_Deep_Scan,Enabled,False
  SetDialogProperty>Dialog_MAIN,Button_Scan,Enabled,False
  Let>SUM_OF_ALL_INPUTS={%DO_A_SCAN_Var_1%+%DO_A_SCAN_Var_2%+%DO_A_SCAN_Var_3%}
  Let>DEEP_SCAN=DO_A_SCAN_Var_1
  Let>PING_SCAN=DO_A_SCAN_Var_2
  Let>INFO_SCAN=DO_A_SCAN_Var_3
  If>PING_SCAN>0
    GoSub>PING_ESPRESSIF_IPS
  Endif>
  If>DEEP_SCAN>0
    GoSub>PING_THE_NETWORK
    Let>SUM_OF_ALL_INPUTS=0
    Let>INFO_SCAN=1
  Endif>
  If>SUM_OF_ALL_INPUTS=0
    GoSub>GET_THE_MAC
  Endif>
  If>INFO_SCAN>0
    GoSub>FETCH_INFO_ABOUT_UNITS
  Endif>
  GoSub>THE_CSV_TO_THE_DIALOG
  SetDialogProperty>Dialog_MAIN,THE_GRID,LoadFromCSV,THE_GRID_CSV
  SetDialogProperty>Dialog_MAIN,Button_Deep_Scan,Enabled,True
  SetDialogProperty>Dialog_MAIN,Button_Scan,Enabled,True
  RegEx>[IPAddress],THE_GRID_CSV,1,,FOUND_IN_ARP,,,
  If>FOUND_IN_ARP>0
    //YES, we found something
  Else>
    Let>EXTRA_TEXT=, please run "Deep scan"
    SetDialogProperty>Dialog_MAIN,STATUS_INFO_BAR,Caption,No ESP Easy units found in ARP table%EXTRA_TEXT%.
    SetDialogProperty>Dialog_MAIN,STATUS_INFO_BAR,Visible,True
    Let>TIME_TO_DISPLAY_MESSAGE=5
  Endif>
END>DO_A_SCAN

SRT>GET_THE_IP_OCTETS
  Separate>ACTIVE_IP_ADDRESS,.,IP_OCTETS
  Let>ACTIVE_SUB_NET=%IP_OCTETS_1%.%IP_OCTETS_2%.%IP_OCTETS_3%
END>GET_THE_IP_OCTETS

SRT>PING_THE_NETWORK
    Let>RP_WINDOWMODE=0
    Let>RP_WAIT=0
    Let>IP_OCTETS_4=0
    Let>IP_OCTETS_4_max=254
    Repeat>IP_OCTETS_4
      Let>IP_OCTETS_4=IP_OCTETS_4+1
      Let>IP_NUMBER=%ACTIVE_SUB_NET%.%IP_OCTETS_4%
      Let>PROGRESS_POSITION={round(100*%IP_OCTETS_4%/%IP_OCTETS_4_max%)}
      SetDialogProperty>Dialog_MAIN,ProgressBar,Position,%PROGRESS_POSITION%
      SetDialogProperty>Dialog_MAIN,STATUS_INFO_BAR,Caption,Pinging %IP_NUMBER% (%IP_OCTETS_4%/%IP_OCTETS_4_max%)
      SetDialogProperty>Dialog_MAIN,STATUS_INFO_BAR,Visible,True
      If>DEBUG_LEVEL>0
        WriteLn>%SCRIPT_DIR%\APP_DATA\PING_RESULTS\PING_%IP_NUMBER%.txt,,########STARTED########
        Let>DEBUG_OUTPUT= | tee '%SCRIPT_DIR%\APP_DATA\PING_RESULTS\PING_%IP_NUMBER%.txt'
        IfNotDirExists>%SCRIPT_DIR%\APP_DATA\PING_RESULTS
          CreateDir>%SCRIPT_DIR%\APP_DATA\PING_RESULTS
        Endif>
        IfFileExists>%SCRIPT_DIR%\APP_DATA\PING_RESULTS\PING_%IP_NUMBER%.txt
          DeleteFile>%SCRIPT_DIR%\APP_DATA\PING_RESULTS\PING_%IP_NUMBER%.txt
        Endif>
      Else>
        Let>DEBUG_OUTPUT=
      Endif>
      Let>RUN_STRING=powershell "test-connection -ComputerName %IP_NUMBER% -count 1 -ErrorAction SilentlyContinue | Format-List%DEBUG_OUTPUT%"
      RunProgram>%RUN_STRING%
    Until>IP_OCTETS_4=IP_OCTETS_4_max
    Let>RP_WAIT=1
    SetDialogProperty>Dialog_MAIN,STATUS_INFO_BAR,Visible,False
    SetDialogProperty>Dialog_MAIN,ProgressBar,Position,0
END>PING_THE_NETWORK

SRT>PING_ESPRESSIF_IPS

END>PING_ESPRESSIF_IPS

SRT>FETCH_INFO_ABOUT_UNITS
  Separate>FOUND_ESPRESSIF_UNITS,%CRLF%,FOUND_ESPRESSIF_UNITS_IP_ARRAY
  If>FOUND_ESPRESSIF_UNITS_IP_ARRAY_count=0
    Let>EXTRA_TEXT=
    If>DEEP_SCAN=1
      Let>EXTRA_TEXT=, please run "Deep scan"
    Endif>
    SetDialogProperty>Dialog_MAIN,STATUS_INFO_BAR,Caption,No ESP Easy units found in ARP table%EXTRA_TEXT%.
  Else>
    SetDialogProperty>Dialog_MAIN,STATUS_INFO_BAR,Visible,True
    Let>TOTAL_COUNT=FOUND_ESPRESSIF_UNITS_IP_ARRAY_count-1
    Let>k_csv=0
    Repeat>k_csv
      Let>k_csv=k_csv+1
      Let>CURRENT_IP=FOUND_ESPRESSIF_UNITS_IP_ARRAY_%k_csv%
      Let>PROGRESS_POSITION={round(100*%k_csv%/%TOTAL_COUNT%)}
      SetDialogProperty>Dialog_MAIN,ProgressBar,Position,%PROGRESS_POSITION%
      SetDialogProperty>Dialog_MAIN,STATUS_INFO_BAR,Caption,Getting info from %CURRENT_IP% (%k_csv%/%TOTAL_COUNT%)
      Let>HTTP_TIMEOUT=5
      Timer>TIMER1
      HTTPRequest>http://%CURRENT_IP%/json,,GET,,JSON_STRING,,,,
      Timer>TIMER2
      Let>TIME_DIFF=TIMER2-TIMER1
      If>JSON_STRING=404 HTTP/1.1 404 Not Found
        //
        Let>BUILD_NUMBER_%k_csv%=?
        Let>GIT_BUILD_NUMBER_%k_csv%=?
        Let>UNIT_NUMBER_%k_csv%=?
        Let>UNIT_NAME_%k_csv%=?
        Let>UNIT_ONLINE_%k_csv%="Offline"
      Else>
        JSONParse>JSON_STRING,System.Build,TEMP_STRING
        If>TEMP_STRING=
          Let>TEMP_STRING=?
        Endif>
        Let>BUILD_NUMBER_%k_csv%=TEMP_STRING
        JSONParse>JSON_STRING,System.Unit,TEMP_STRING
        If>TEMP_STRING=
          Let>TEMP_STRING=?
        Endif>
        Let>UNIT_NUMBER_%k_csv%=TEMP_STRING
        JSONParse>JSON_STRING,System.Name,TEMP_STRING
        If>TEMP_STRING=
          Let>TEMP_STRING=?
        Endif>
        Let>UNIT_NAME_%k_csv%=TEMP_STRING
        JSONParse>JSON_STRING,System.Git Build,TEMP_STRING
        If>TEMP_STRING=
          Let>TEMP_STRING=?
        Endif>
        Let>GIT_BUILD_NUMBER_%k_csv%=TEMP_STRING
        Let>UNIT_ONLINE_%k_csv%="Online"
      Endif>
      If>TIME_DIFF>5000
        Let>UNIT_ONLINE_%k_csv%="Timed out"
      Endif>
      Let>TEMP_NAME=UNIT_NAME_%k_csv%
      Let>TEMP_NUMBER=UNIT_NUMBER_%k_csv%
      Let>TEMP_BUILD=BUILD_NUMBER_%k_csv%
      Let>TEMP_GIT=GIT_BUILD_NUMBER_%k_csv%
      Let>TEMP_ONLINE=UNIT_ONLINE_%k_csv%
      Let>TEMP_ROW_INFO_%k_csv%=,%TEMP_NAME%,%TEMP_NUMBER%,%TEMP_BUILD%,%TEMP_GIT%,%TEMP_ONLINE%
    Until>k_csv=TOTAL_COUNT
    SetDialogProperty>Dialog_MAIN,STATUS_INFO_BAR,Visible,False
    SetDialogProperty>Dialog_MAIN,ProgressBar,Position,0
  Endif>
END>FETCH_INFO_ABOUT_UNITS

SRT>GET_THE_MAC
  IfFileExists>%SCRIPT_DIR%\APP_DATA\ARP_INFO.txt
    DeleteFile>%SCRIPT_DIR%\APP_DATA\ARP_INFO.txt
  Endif>
  Let>RUN_STRING=cmd /c arp -a > "%SCRIPT_DIR%\APP_DATA\ARP_INFO.txt"
  Let>RP_WINDOWMODE=0
  Let>RP_WAIT=1
  RunProgram>%RUN_STRING%
  ReadFile>%SCRIPT_DIR%\APP_DATA\ARP_INFO.txt,FILE_CONTENT
  IfFileExists>%SCRIPT_DIR%\APP_DATA\ARP_INFO.txt
    DeleteFile>%SCRIPT_DIR%\APP_DATA\ARP_INFO.txt
  Endif>
  Let>REGEX_PATTERN=%IP_OCTETS_1%\.%IP_OCTETS_2%\.%IP_OCTETS_3%\..[0123456789]{0,2} {0,}.{2}-.{2}-.{2}-.{2}-.{2}-.{2}
  RegEx>REGEX_PATTERN,FILE_CONTENT,,FOUND_MAC_ADDRESSES,FOUND_MAC_ADDRESSES_num,,,
  Let>FOUND_ESPRESSIF_UNITS=
  If>FOUND_MAC_ADDRESSES_num>0
    Let>k_espressif=0
    Repeat>k_espressif
      Let>k_espressif=k_espressif+1
      Let>TEMP_CHECK=FOUND_MAC_ADDRESSES_%k_espressif%
      RegEx> ,TEMP_CHECK,0,,,1,%CRLF%,TEMP_CHECK
      Separate>TEMP_CHECK,%CRLF%,TEMP_CHECK_ARRAY
      Let>CURRENT_IP_NUMBER=TEMP_CHECK_ARRAY_1
      Trim>CURRENT_IP_NUMBER,CURRENT_IP_NUMBER
      Let>CURENT_MAC_ADDRES=TEMP_CHECK_ARRAY_%TEMP_CHECK_ARRAY_count%
      Trim>CURENT_MAC_ADDRES,CURENT_MAC_ADDRES
      UpperCase>CURENT_MAC_ADDRES,CURENT_MAC_ADDRES
      RegEx>REGEX_PATTERN_MAC,CURENT_MAC_ADDRES,0,,IS_IT_FOUND,,,
      If>IS_IT_FOUND>0
        Let>FOUND_ESPRESSIF_UNITS=%FOUND_ESPRESSIF_UNITS%%CURRENT_IP_NUMBER%%CRLF%
      Endif>
    Until>k_espressif=FOUND_MAC_ADDRESSES_num
  Else>
    //NOTHING FOUND IN ARP...
  Endif>
END>GET_THE_MAC

SRT>CONCAT_THE_MAC
    LabelToVar>OUI_INFORMATION,MAC_ADDRESSES,1,1,
    Let>REGEX_PATTERN=(?<=[0123456789]{3}: ).{2}-.{2}-.{2}
    RegEx>REGEX_PATTERN,MAC_ADDRESSES,,MAC_ADDRESSES_ARRAY,MAC_ADDRESSES_ARRAY_num,,,
    If>MAC_ADDRESSES_ARRAY_num>0
      Let>REGEX_PATTERN_MAC=
      Let>k_mac_list=0
      Repeat>k_mac_list
        Let>k_mac_list=k_mac_list+1
        Let>TEMP_MAC=MAC_ADDRESSES_ARRAY_%k_mac_list%
        Let>REGEX_PATTERN_MAC=%REGEX_PATTERN_MAC%%TEMP_MAC%
        If>k_mac_list=MAC_ADDRESSES_ARRAY_num
          //Do nothing
        Else>
          Let>REGEX_PATTERN_MAC=%REGEX_PATTERN_MAC%|
        Endif>
      Until>k_mac_list=MAC_ADDRESSES_ARRAY_num
    Endif>
END>CONCAT_THE_MAC

SRT>THE_CSV_TO_THE_DIALOG
  Let>THE_GRID_CSV="IP","Name","Number","Version","Git version","Status"
  Let>REGEX_PATTERN=,
  RegEx>REGEX_PATTERN,THE_GRID_CSV,0,,NUMBER_OF_COLUMNS,0,,
  Let>NUMBER_OF_COLUMNS=NUMBER_OF_COLUMNS+1
  SetDialogProperty>Dialog_MAIN,THE_GRID,ColCount,%NUMBER_OF_COLUMNS%
  Separate>FOUND_ESPRESSIF_UNITS,%CRLF%,FOUND_ESPRESSIF_UNITS_IP_ARRAY
  If>FOUND_ESPRESSIF_UNITS_IP_ARRAY_count>0
    Let>k_csv=0
    Repeat>k_csv
      Let>k_csv=k_csv+1
      Let>TEMP_ROW=FOUND_ESPRESSIF_UNITS_IP_ARRAY_%k_csv%
      If>k_csv=FOUND_ESPRESSIF_UNITS_IP_ARRAY_count
        Let>THE_GRID_CSV=%THE_GRID_CSV%%TEMP_ROW%
      Else>
        Let>THE_GRID_CSV=%THE_GRID_CSV%%CRLF%
        Let>THE_GRID_CSV=%THE_GRID_CSV%%TEMP_ROW%
      Endif>
      Assigned>TEMP_ROW_INFO_%k_csv%,IS_IT_ASSIGNED
      If>IS_IT_ASSIGNED=FALSE
        Let>TEMP_STRING=
      Else>
        Let>TEMP_STRING=TEMP_ROW_INFO_%k_csv%
      Endif>
      ConCat>THE_GRID_CSV,TEMP_STRING
    Until>k_csv=FOUND_ESPRESSIF_UNITS_IP_ARRAY_count
  Else>
    Let>INFO_SCAN=1
    Let>DEEP_SCAN=1
  Endif>
END>THE_CSV_TO_THE_DIALOG

SRT>GET_LOCAL_IP
Let>RP_WAIT=1
Let>RP_WINDOWMODE=0
IfDirExists>%SCRIPT_DIR%\APP_DATA
  //Do nothing
Else>
  CreateDir>%SCRIPT_DIR%\APP_DATA
Endif>
IfFileExists>%SCRIPT_DIR%\APP_DATA\LOCAL_IP.txt
  DeleteFile>%SCRIPT_DIR%\APP_DATA\LOCAL_IP.txt
Endif>
RunProgram>cmd /c ipconfig | Find "IPv4 Address" > CreateDir>"%SCRIPT_DIR%\APP_DATA\LOCAL_IP.txt"
ReadFile>%SCRIPT_DIR%\APP_DATA\LOCAL_IP.txt,FILE_CONTENT
RegEx>[IPAddress],FILE_CONTENT,1,FOUND_IP,,,,
Let>ACTIVE_IP_ADDRESS=FOUND_IP_1
IfFileExists>%SCRIPT_DIR%\APP_DATA\LOCAL_IP.txt
  DeleteFile>%SCRIPT_DIR%\APP_DATA\LOCAL_IP.txt
Endif>
Let>RP_WAIT=0
END>GET_LOCAL_IP

/*
OUI_INFORMATION:
#######MAC OUI to be able to search ESP units######
###Found here: http://standards-oui.ieee.org/oui.txt ###
###Scanning the IP with this: arp -a XXX.XXX.XXX.XXX##
###RegEx: (?:.){8}(?= {3}\(hex\)\t{2}Espressif Inc\.)##
#############Espressif Inc. OUI#############
001: 54-5A-A6
002: 24-0A-C4
003: D8-A0-1D
004: EC-FA-BC
005: B4-E6-2D
006: A0-20-A6
007: 90-97-D5
008: 18-FE-34
009: 60-01-94
010: 2C-3A-E8
011: A4-7B-9D
012: DC-4F-22
013: BC-DD-C2
014: 5C-CF-7F
015: AC-D0-74
016: 30-AE-A4
017: 24-B2-DE
018: 68-C6-3A
*/
